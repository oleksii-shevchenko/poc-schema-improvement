plugins {
    id 'java'
    id 'com.google.protobuf' version '0.8.17'
    id 'maven-publish'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "com.google.protobuf:protobuf-java:3.18.1"

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

test {
    useJUnitPlatform()
}
protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.0.0'
    }
}

sourceSets {
    main {
        proto {
            srcDir 'src/main/protobuf'
        }
    }
}

ext.deploymentVersion = {
    if ("true" == System.getenv("CI")) {
        var minorVersion = property("minor.version")
        var patchVersion = property("patch.version")
        var versionType = hasProperty("release") ? "release" : "snapshot"
        return "1.${minorVersion}.${patchVersion}-${versionType}"
    } else {
        return "0.0.1-local"
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/${project.property("gh.owner")}/${project.property("gh.repo")}")
            credentials {
                username System.getenv("GITHUB_ACTOR")
                password System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            version = project.deploymentVersion()
            groupId = 'dev.flanker'
            artifactId = 'proto-messages'
            from components.java
        }
    }
}